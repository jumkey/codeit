import org.jetbrains.changelog.Changelog
import org.jetbrains.changelog.ExtensionsKt

plugins {
    id 'org.jetbrains.intellij' version '1.13.2'
    id 'org.jetbrains.changelog' version '2.0.0'
    id 'java'
}

group 'org.cafeboy.idea.plugin'
version '1.0.4'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
    mavenCentral()
}

dependencies {
    implementation 'com.google.zxing:core:3.5.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.2.4'
}
patchPluginXml {
    sinceBuild = '203'
    untilBuild = ''
    pluginDescription = ExtensionsKt.markdownToHTML("""## Description
A plug-in that generates a two-dimensional barcode of a specific text.
## Features
- Create QR code.
- Read QR code from the screen.
- Parse QR code picture into text.
## How to use
- Right-click the selected text to generate a QR code
- Open Codeit tool window to discover more features
""")
    changeNotes = changelog.renderItem(
            changelog
                    .getLatest()
                    .withHeader(false)
                    .withEmptySections(false),
            Changelog.OutputType.HTML
    )
}
test {
    useJUnitPlatform()
}
