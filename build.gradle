import org.jetbrains.changelog.ExtensionsKt

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.2.0'
    id 'org.jetbrains.changelog' version '1.2.1'
}

group 'org.cafeboy.idea.plugin'
version '1.0.2-SNAPSHOT'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    maven { url "https://maven.aliyun.com/repository/public/" }
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
    mavenCentral()
}

dependencies {
    implementation 'com.google.zxing:core:3.4.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.2.4'
}
patchPluginXml {
    sinceBuild = '203'
    untilBuild = provider { null }
    pluginDescription = ExtensionsKt.markdownToHTML("""## Description
A plug-in that generates a two-dimensional barcode of a specific text.
## Features
- Create QR code.
- Read QR code from the screen.
- Parse QR code picture into text.
## How to use
- Right-click the selected text to generate a QR code
- Open Codeit tool window to discover more features
""")
    changeNotes = changelog.getLatest().toHTML()
}
test {
    useJUnitPlatform()
}
